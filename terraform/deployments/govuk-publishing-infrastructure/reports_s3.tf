resource "aws_s3_bucket" "publisher_csvs" {
  bucket        = "govuk-${var.govuk_environment}-publisher-csvs"
  force_destroy = var.force_destroy
  tags = {
    System = "Publisher"
    Name   = "Reports generated by Publisher"
  }
}

resource "aws_s3_bucket" "support_api_csvs" {
  bucket        = "govuk-${var.govuk_environment}-support-api-csvs"
  force_destroy = var.force_destroy
  tags = {
    System      = "Support"
    Name        = "govuk-${var.govuk_environment}-support-api-csvs"
    Description = "Reports generated by the Support app"
  }
}

resource "aws_s3_bucket_logging" "support_api_csvs" {
  bucket        = aws_s3_bucket.support_api_csvs.id
  target_bucket = "govuk-${var.govuk_environment}-aws-logging"
  target_prefix = "s3/govuk-${var.govuk_environment}-support-api-csvs/"
}

# TODO: the govuk-publishing-api-event-log-* buckets don't seem to be defined
# in alphagov/govuk-aws; define them somewhere appropriate (this module would
# do for now) and import them.

data "aws_iam_policy_document" "write_reports_to_s3" {
  statement {
    actions = ["s3:GetBucketLocation", "s3:ListBucket"]
    resources = [
      aws_s3_bucket.publisher_csvs.arn,
      "arn:aws:s3:::govuk-${var.govuk_environment}-content-data-csvs",
      "arn:aws:s3:::govuk-${var.govuk_environment}-content-data-siteimprove-sitemaps",
      "arn:aws:s3:::govuk-${var.govuk_environment}-specialist-publisher-csvs",
      "arn:aws:s3:::govuk-${var.govuk_environment}-support-api-csvs",
      "arn:aws:s3:::govuk-${var.govuk_environment}-whitehall-csvs",
      "arn:aws:s3:::govuk-publishing-api-event-log-${var.govuk_environment}",
    ]
  }

  statement {
    actions = [
      "s3:*MultipartUpload*",
      "s3:*Object",
      "s3:*ObjectAcl",
      "s3:*ObjectVersion",
      "s3:GetObject*Attributes"
    ]
    resources = [
      "${aws_s3_bucket.publisher_csvs.arn}/*",
      "arn:aws:s3:::govuk-${var.govuk_environment}-content-data-csvs/*",
      "arn:aws:s3:::govuk-${var.govuk_environment}-content-data-siteimprove-sitemaps/*",
      "arn:aws:s3:::govuk-${var.govuk_environment}-specialist-publisher-csvs/*",
      "arn:aws:s3:::govuk-${var.govuk_environment}-support-api-csvs/*",
      "arn:aws:s3:::govuk-${var.govuk_environment}-whitehall-csvs/*",
      "arn:aws:s3:::govuk-publishing-api-event-log-${var.govuk_environment}",
    ]
  }
}

resource "aws_iam_policy" "write_reports_to_s3" {
  name   = "ReadWriteReportBuckets"
  policy = data.aws_iam_policy_document.write_reports_to_s3.json
}

resource "aws_iam_role_policy_attachment" "write_reports_to_s3" {
  role       = data.tfe_outputs.cluster_infrastructure.nonsensitive_values.worker_iam_role_name
  policy_arn = aws_iam_policy.write_reports_to_s3.arn
}
